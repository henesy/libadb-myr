/*
	Process attrdb(6) files
*/

use std
use bio

pkg adb =
	const ingest	: (f : byte[:]	-> std.result(db,			byte[:]))

	/* Types */

	// An attribute=value pair
	type attribute = struct
		key		: byte[:]
		value	: byte[:]
	;;

	// A tuple composed of attributes
	type tuple = struct
		pairs : attribute[:]
	;;

	// An individual record composed of tuples
	type record = struct
		tuples : tuple[:]
	;;

	// A parsed attrdb database
	type db = struct
		records : record[:]
	;;

	// The results from a query
	type query = union
		`Some
		`None
	;;

	/* State */

	var chatty = false
;;

/* ???

	Are traits in myr powerful enough to implement find(@f) as :: findable on top of:

		{tuple[:], record[:], attribute[:]}

	???
*/


// Open and parse a file
const ingest = {path : byte[:]
	var f, db

	match bio.open(path, bio.Rd)
	| `std.Ok	file:
		f = file

	| `std.Err	e:
		-> `std.Err e
	;;

	// States for parsing state machine
	type states = union
	`SingleQuote	uint64		// The starting character point for the quote
	`DoubleQuote	uint64		// ^
	`Comment
	`Newline
	`Key
	`Value
	;;

	// Current state
	var state	= `Newline

	// Last state
	var last	= state

	// Current line for errors - tick by passing \n
	var line	= 1

	// Current character for errors - tick each iteration
	var cn		= 0

	// String builder buffer
	var buf		= std.mksb()	// Memory leak? - need init for compiler to ☺

	// Other builders
	var pair	: attribute
	var tuple	: tuple
	var record	: record


	// Parse into db
	for c : bio.bychar(f)
		last = state

		cn++

		if c == '\n'
			line++
			cn = 0
		;;

		if chatty
			// Nice printing of each character
			std.put("→ ")
			match c
			| '\n':
				std.put("\\n")
			| '\t':
				std.put("\\t")
			| ' ':
				std.put("_")
			| _:
				std.put("{}", c)
			;;
			std.put("				({})\n", state)
		;;

		match state
		| `Comment:
			// Read through until end of line
			if c == '\n'
				state = `Newline
			;;

			continue
		| _:
			;
		;;

		match c
		| '#':
			// Allow comments at the beginning of lines and after values
			match state
			| `Newline:
				;
			| `Value:
				;
			| _:
				-> `std.Err std.fmt("invalid comment beginning at {}:{}:{}", path, line, cn)
			;;

			state = `Comment

		| '\n':
			var str = std.sbpeek(buf)
			std.sbtrim(buf, 0)

			if str.len < 1
				// Empty string, discard value
				;
			else
				std.put("» finished value (\\n): {}\n", str)
				pair.value = str
			;;

			state = `Newline

		| '\'':
			match state
			| `SingleQuote origin:
				// In a quote already

				match bio.peekc(f)
				| `std.Ok	next:
					match next
					| '\'':
						// A '' in a quote
						if ! std.sbputc(buf, '\'')
							-> `std.Err std.fmt("could not insert ' at {}:{}:{}", path, line, cn)
						;;

						// Consume the next quote
						bio.getc(f)
					| _:
						// Ending a quote
						var str = std.sbpeek(buf)
						std.sbtrim(buf, 0)

						if str.len < 1
							// Empty string, discard value
						else
							std.put("» finished value ('): {}\n", str)
							pair.value = str
						;;

						state = `Key
						buf = std.mksb()
					;;
				| `std.Err	e:
					-> `std.Err std.fmt("single quote peek failed at {}:{}:{}", path, line, cn)
				;;

			| `Value:
				// Beginning a quote
				state = `SingleQuote cn

			| `DoubleQuote origin:
				std.sbputc(buf, c)

			| _:
				-> `std.Err std.fmt("unexpected single quote at {}:{}:{}", path, line, cn)
			;;

		| '\"':
			match state
			| `DoubleQuote origin:
				// In a quote already

				match bio.peekc(f)
				| `std.Ok	next:
					match next
					| '\"':
						// A "" in a quote
						if ! std.sbputc(buf, '\"')
							-> `std.Err std.fmt("could not insert \" at {}:{}:{}", path, line, cn)
						;;

						// Consume the next quote
						bio.getc(f)
					| _:
						// Ending a quote
						var str = std.sbpeek(buf)
						std.sbtrim(buf, 0)

						if str.len < 1
							// Empty string, discard value
						else
							std.put("» finished value (\"): {}\n", str)
							pair.value = str
						;;

						state = `Key
						buf = std.mksb()
					;;
				| `std.Err	e:
					-> `std.Err std.fmt("double quote peek failed at {}:{}:{}", path, line, cn)
				;;

			| `Value:
				// Beginning a quote
				state = `DoubleQuote cn

			| `SingleQuote origin:
				std.sbputc(buf, c)

			| _:
				-> `std.Err std.fmt("unexpected double quote at {}:{}:{}", path, line, cn)
			;;

		| ' ':

			match state
			| `Newline:
				// This is necessary, apparently
				// Start a new sub-tuple of a record
				std.put("»» start sub-tuple (_)\n")
				state = `Key
				buf = std.mksb()

			| `Key:
				// Allow to passthrough, might be a mistake?
				;

			| `Value:
				var str = std.sbpeek(buf)
				std.sbtrim(buf, 0)

				if str.len < 1
					// Empty string, discard value
				else
					std.put("» finished value (_): {}\n", str)
					pair.value = str
				;;

				state = `Key
				buf = std.mksb()

			| `SingleQuote origin:
				std.sbputc(buf, c)

			| `DoubleQuote origin:
				std.sbputc(buf, c)

			| _:
				-> `std.Err std.fmt("invalid space at {}:{}:{}", path, line, cn)
			;;

		| '\t':
			match state
			| `Newline:
				// This is necessary, apparently
				// Start a new sub-tuple of a record
				state = `Key
				buf = std.mksb()
				std.put("»» start sub-tuple (\\t)\n")

			| `Key:
				// Allow to passthrough, might be a mistake?
				;

			| `Value:
				var str = std.sbpeek(buf)
				std.sbtrim(buf, 0)

				if str.len < 1
					// Empty string, discard value
				else
					std.put("» finished value (\\t): {}\n", str)
					pair.value = str
				;;

				state = `Key
				buf = std.mksb()

			| `SingleQuote origin:
				std.sbputc(buf, c)

			| `DoubleQuote origin:
				std.sbputc(buf, c)

			| _:
				-> `std.Err std.fmt("invalid tab at {}:{}:{}", path, line, cn)
			;;

		| '=':
			var str = std.sbpeek(buf)
			std.sbtrim(buf, 0)

			if str.len < 1
				// Empty string, disallowed for keys
				-> `std.Err std.fmt("empty key at {}:{}:{}", path, line, cn)
			else
				std.put("» finished key (=): {}\n", str)
				pair.key = str
			;;

			state = `Value
			buf = std.mksb()
		| _:
			// A key or value text segment
			match state
			| `Key:
				if ! std.sbputc(buf, c)
					-> `std.Err std.fmt("could not insert key char at {}:{}:{}", path, line, cn)
				;;

			| `Value:
				std.sbputc(buf, c)

			| `SingleQuote	origin:
				std.sbputc(buf, c)

			| `DoubleQuote	origin:
				std.sbputc(buf, c)

			| `Newline:
				// Start a new record
				std.put("»» start record\n")
				std.put("»» start master tuple\n")
				state = `Key
				std.sbfree(buf)
				buf = std.mksb()
				std.sbputc(buf, c)

			| _:
				-> `std.Err std.fmt("unexpected character at {}:{}:{}", path, line, cn)
			;;
		;;
	;;

	// TODO - if there is a tuple (or key= only pair) dangling, be sure to append it
	// Check key.len?

	-> `std.Ok db
}
